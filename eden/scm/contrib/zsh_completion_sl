#compdef sl

# Zsh completion script for mercurial.  Rename this file to _sl and copy
# it into your zsh function path (/usr/share/zsh/site-functions for
# instance)
#
# If you do not want to install it globally, you can copy it somewhere
# else and add that directory to $fpath. This must be done before
# compinit is called. If the file is copied to ~/.zsh.d, your ~/.zshrc
# file could look like this:
#
# fpath=("$HOME/.zsh.d" $fpath)
# autoload -U compinit
# compinit
#
# Copyright (C) 2005, 2006 Steve Borho <steve@borho.org>
# Copyright (C) 2006-10 Brendan Cully <brendan@kublai.com>
#
# Permission is hereby granted, without written agreement and without
# licence or royalty fees, to use, copy, modify, and distribute this
# software and to distribute modified versions of this software for any
# purpose, provided that the above copyright notice and the following
# two paragraphs appear in all copies of this software.
#
# In no event shall the authors be liable to any party for direct,
# indirect, special, incidental, or consequential damages arising out of
# the use of this software and its documentation, even if the authors
# have been advised of the possibility of such damage.
#
# The authors specifically disclaim any warranties, including, but not
# limited to, the implied warranties of merchantability and fitness for
# a particular purpose.  The software provided hereunder is on an "as
# is" basis, and the authors have no obligation to provide maintenance,
# support, updates, enhancements, or modifications.

emulate -LR zsh
setopt extendedglob

local curcontext="$curcontext" state line
typeset -A _sl_cmd_globals

_sl() {
  local cmd _sl_root
  integer i=2
  _sl_cmd_globals=()

  while (( i < $#words ))
  do
    case "$words[$i]" in
      -R|--repository)
        eval _sl_root="$words[$i+1]"
        _sl_cmd_globals+=("$words[$i]" "$_sl_root")
        (( i += 2 ))
        continue
      ;;
      -R*)
        _sl_cmd_globals+="$words[$i]"
        eval _sl_root="${words[$i]#-R}"
       (( i++ ))
       continue
      ;;
      --cwd|--config)
        # pass along arguments to sl completer
        _sl_cmd_globals+=("$words[$i]" "$words[$i+1]")
        (( i += 2 ))
        continue
      ;;
      -*)
        # skip option
        (( i++ ))
        continue
      ;;
    esac
    if [[ -z "$cmd" ]]
    then
      cmd="$words[$i]"
      words[$i]=()
      (( CURRENT-- ))
    fi
    (( i++ ))
  done

  if [[ -z "$cmd" ]]
  then
    _arguments -s -w : $_sl_global_opts \
    ':mercurial command:_sl_commands'
    return
  fi

  # resolve abbreviations and aliases
  if ! (( $+functions[_sl_cmd_${cmd}] ))
  then
    local cmdexp
    (( $#_sl_cmd_list )) || _sl_get_commands

    cmdexp=$_sl_cmd_list[(r)${cmd}*]
    if [[ $cmdexp == $_sl_cmd_list[(R)${cmd}*] ]]
    then
      # might be nice to rewrite the command line with the expansion
      cmd="$cmdexp"
    fi
    if [[ -n $_sl_alias_list[$cmd] ]]
    then
      cmd=$_sl_alias_list[$cmd]
    fi
  fi

  curcontext="${curcontext%:*:*}:sl-${cmd}:"

  zstyle -s ":completion:$curcontext:" cache-policy update_policy

  if [[ -z "$update_policy" ]]
  then
    zstyle ":completion:$curcontext:" cache-policy _sl_cache_policy
  fi

  if (( $+functions[_sl_cmd_${cmd}] ))
  then
    _sl_cmd_${cmd}
  else
    # complete unknown commands normally
    _arguments -s -w : $_sl_global_opts \
      '*:files:_sl_files'
  fi
}

_sl_cache_policy() {
  typeset -a old

  # cache for a minute
  old=( "$1"(mm+10) )
  (( $#old )) && return 0

  return 1
}

_sl_get_commands() {
  typeset -ga _sl_cmd_list
  typeset -gA _sl_alias_list
  local hline cmd cmdalias

  _call_program sl sl debugcomplete -v | while read -A hline
  do
    cmd=$hline[1]
    _sl_cmd_list+=($cmd)

    for cmdalias in $hline[2,-1]
    do
      _sl_cmd_list+=($cmdalias)
      _sl_alias_list+=($cmdalias $cmd)
    done
  done
}

_sl_commands() {
  (( $#_sl_cmd_list )) || _sl_get_commands
  _describe -t commands 'mercurial command' _sl_cmd_list
}

_sl_revrange() {
  compset -P 1 '*:'
  _sl_labels "$@"
}

_sl_labels() {
  labels=("${(f)$(_sl_cmd debugnamecomplete)}")
  (( $#labels )) && _describe -t labels 'labels' labels
}

_sl_bookmarks() {
  typeset -a bookmark bookmarks

  _sl_cmd bookmarks | while read -A bookmark
  do
    if test -z ${bookmark[-1]:#[0-9]*}
    then
      bookmarks+=($bookmark[-2])
    fi
  done
  (( $#bookmarks )) && _describe -t bookmarks 'bookmarks' bookmarks
}

_sl_branches() {
  typeset -a branches
  local branch

  _sl_cmd branches | while read branch
  do
    branches+=(${branch/ #[0-9]#:*})
  done
  (( $#branches )) && _describe -t branches 'branches' branches
}

# likely merge candidates
_sl_mergerevs() {
  typeset -a heads
  local myrev

  heads=(${(f)"$(_sl_cmd heads --template '{rev}:{branch}\\n')"})
  # exclude own revision
  myrev=$(_sl_cmd log -r . --template '{rev}:{branch}\\n')
  heads=(${heads:#$myrev})

  (( $#heads )) && _describe -t heads 'heads' heads

  branches=(${(f)"$(_sl_cmd heads --template '{branch}\\n')"})
  # exclude own revision
  myrev=$(_sl_cmd log -r . --template '{branch}\\n')
  branches=(${branches:#$myrev})

  (( $#branches )) && _describe -t branches 'branches' branches
}

_sl_files() {
  if [[ -n "$_sl_root" ]]
  then
    [[ -d "$_sl_root/.sl" ]] || return
    case "$_sl_root" in
      /*)
        _files -W $_sl_root
      ;;
      *)
        _files -W $PWD/$_sl_root
      ;;
    esac
  else
    _files
  fi
}

_sl_status() {
  [[ -d $PREFIX ]] || PREFIX=$PREFIX:h
  status_files=(${(ps:\0:)"$(_sl_cmd status -0n$1 ./$PREFIX)"})
}

_sl_unknown() {
  typeset -a status_files
  _sl_status u
  _wanted files expl 'unknown files' _multi_parts / status_files
}

_sl_missing() {
  typeset -a status_files
  _sl_status d
  _wanted files expl 'missing files' _multi_parts / status_files
}

_sl_modified() {
  typeset -a status_files
  _sl_status m
  _wanted files expl 'modified files' _multi_parts / status_files
}

_sl_resolve() {
  local rstate rpath

  [[ -d $PREFIX ]] || PREFIX=$PREFIX:h

  _sl_cmd resolve -l ./$PREFIX | while read rstate rpath
  do
    [[ $rstate == 'R' ]] && resolved_files+=($rpath)
    [[ $rstate == 'U' ]] && unresolved_files+=($rpath)
  done
}

_sl_resolved() {
  typeset -a resolved_files unresolved_files
  _sl_resolve
  _wanted files expl 'resolved files' _multi_parts / resolved_files
}

_sl_unresolved() {
  typeset -a resolved_files unresolved_files
  _sl_resolve
  _wanted files expl 'unresolved files' _multi_parts / unresolved_files
}

_sl_config() {
    typeset -a items
    items=(${${(%f)"$(_call_program sl sl showconfig)"}%%\=*})
    (( $#items )) && _describe -t config 'config item' items
}

_sl_addremove() {
  _alternative 'files:unknown files:_sl_unknown' \
    'files:missing files:_sl_missing'
}

_sl_ssh_urls() {
  if [[ -prefix */ ]]
  then
    if zstyle -T ":completion:${curcontext}:files" remote-access
    then
      local host=${PREFIX%%/*}
      typeset -a remdirs
      compset -p $(( $#host + 1 ))
      local rempath=${(M)PREFIX##*/}
      local cacheid="sl:${host}-${rempath//\//_}"
      cacheid=${cacheid%[-_]}
      compset -P '*/'
      if _cache_invalid "$cacheid" || ! _retrieve_cache "$cacheid"
      then
        remdirs=(${${(M)${(f)"$(_call_program files ssh -a -x $host ls -1FL "${(q)rempath}")"}##*/}%/})
        _store_cache "$cacheid" remdirs
      fi
      _describe -t directories 'remote directory' remdirs -S/
    else
      _message 'remote directory'
    fi
  else
    if compset -P '*@'
    then
      _hosts -S/
    else
      _alternative 'hosts:remote host name:_hosts -S/' \
        'users:user:_users -S@'
    fi
  fi
}

_sl_urls() {
  if compset -P bundle://
  then
    _files
  elif compset -P ssh://
  then
    _sl_ssh_urls
  elif [[ -prefix *: ]]
  then
    _urls
  else
    local expl
    compset -S '[^:]*'
    _wanted url-schemas expl 'URL schema' compadd -S '' - \
      http:// https:// ssh:// bundle://
  fi
}

_sl_paths() {
  typeset -a paths pnames
  _sl_cmd paths | while read -A pnames
  do
    paths+=($pnames[1])
  done
  (( $#paths )) && _describe -t path-aliases 'repository alias' paths
}

_sl_remote() {
  _alternative 'path-aliases:repository alias:_sl_paths' \
    'directories:directory:_files -/' \
    'urls:URL:_sl_urls'
}

_sl_clone_dest() {
  _alternative 'directories:directory:_files -/' \
    'urls:URL:_sl_urls'
}

_sl_add_help_topics=(
    config dates diffs environment extensions filesets glossary slignore
    merge-tools multirevs obsolescence patterns phases revisions revsets
    subrepos templating urls
)

_sl_help_topics() {
    local topics
    (( $#_sl_cmd_list )) || _sl_get_commands
    topics=($_sl_cmd_list $_sl_add_help_topics)
    _describe -t help_topics 'help topics' topics
}

# Common options
_sl_global_opts=(
    '(--repository -R)'{-R+,--repository=}'[repository root directory]:repository:_files -/'
    '--cwd[change working directory]:new working directory:_files -/'
    '(--noninteractive -y)'{-y,--noninteractive}'[do not prompt, assume yes for any required answers]'
    '(--verbose -v)'{-v,--verbose}'[enable additional output]'
    '*--config[set/override config option]:defined config items:_sl_config'
    '(--quiet -q)'{-q,--quiet}'[suppress output]'
    '(--help -h)'{-h,--help}'[display help and exit]'
    '--debug[debug mode]'
    '--debugger[start debugger]'
    '--encoding[set the charset encoding]'
    '--encodingmode[set the charset encoding mode]'
    '--lsprof[print improved command execution profile]'
    '--traceback[print traceback on exception]'
    '--time[time how long the command takes]'
    '--profile[profile]'
    '--version[output version information and exit]'
)

_sl_pat_opts=(
  '*'{-I+,--include=}'[include files matching the given patterns]:dir:_files -W $(_sl_cmd root) -/'
  '*'{-X+,--exclude=}'[exclude files matching the given patterns]:dir:_files -W $(_sl_cmd root) -/')

_sl_clone_opts=(
  $_sl_remote_opts
  '(--noupdate -U)'{-U,--noupdate}'[do not update the new working directory]'
  '--pull[use pull protocol to copy metadata]'
  '--uncompressed[use uncompressed transfer (fast over LAN)]')

_sl_date_user_opts=(
  '(--currentdate -D)'{-D,--currentdate}'[record the current date as commit date]'
  '(--currentuser -U)'{-U,--currentuser}'[record the current user as committer]'
  '(--date -d)'{-d+,--date=}'[record the specified date as commit date]:date:'
  '(--user -u)'{-u+,--user=}'[record the specified user as committer]:user:')

_sl_gitlike_opts=(
  '(--git -g)'{-g,--git}'[use git extended diff format]')

_sl_diff_opts=(
  $_sl_gitlike_opts
  '(--text -a)'{-a,--text}'[treat all files as text]'
  '--nodates[omit dates from diff headers]')

_sl_mergetool_opts=(
  '(--tool -t)'{-t+,--tool=}'[specify merge tool]:tool:')

_sl_dryrun_opts=(
  '(--dry-run -n)'{-n,--dry-run}'[do not perform actions, just print output]')

_sl_ignore_space_opts=(
  '(--ignore-all-space -w)'{-w,--ignore-all-space}'[ignore white space when comparing lines]'
  '(--ignore-space-change -b)'{-b,--ignore-space-change}'[ignore changes in the amount of white space]'
  '(--ignore-blank-lines -B)'{-B,--ignore-blank-lines}'[ignore changes whose lines are all blank]')

_sl_style_opts=(
  '--style[display using template map file]:'
  '--template[display with template]:')

_sl_log_opts=(
  $_sl_global_opts $_sl_style_opts $_sl_gitlike_opts
  '(--limit -l)'{-l+,--limit=}'[limit number of changes displayed]:'
  '(--no-merges -M)'{-M,--no-merges}'[do not show merges]'
  '(--patch -p)'{-p,--patch}'[show patch]'
  '--stat[output diffstat-style summary of changes]'
)

_sl_commit_opts=(
  '(-m --message -l --logfile --edit -e)'{-e,--edit}'[edit commit message]'
  '(-e --edit -l --logfile --message -m)'{-m+,--message=}'[use <text> as commit message]:message:'
  '(-e --edit -m --message --logfile -l)'{-l+,--logfile=}'[read the commit message from <file>]:log file:_files')

_sl_remote_opts=(
  '(--ssh -e)'{-e+,--ssh=}'[specify ssh command to use]:'
  '--remotecmd[specify sl command to run on the remote side]:')

_sl_branch_bmark_opts=(
  '(--bookmark -B)'{-B+,--bookmark=}'[specify bookmark(s)]:bookmark:_sl_bookmarks'
  '(--branch -b)'{-b+,--branch=}'[specify branch(es)]:branch:_sl_branches'
)

_sl_subrepos_opts=(
  '(--subrepos -S)'{-S,--subrepos}'[recurse into subrepositories]')

_sl_cmd() {
  _call_program sl slPLAIN=1 sl "$_sl_cmd_globals[@]" "$@" 2> /dev/null
}

_sl_cmd_add() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_dryrun_opts $_sl_subrepos_opts \
  '*:unknown files:_sl_unknown'
}

_sl_cmd_addremove() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_dryrun_opts \
  '(--similarity -s)'{-s+,--similarity=}'[guess renamed files by similarity (0<=s<=100)]:' \
  '*:unknown or missing files:_sl_addremove'
}

_sl_cmd_annotate() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts \
  '(--rev -r)'{-r+,--rev=}'[annotate the specified revision]:revision:_sl_labels' \
  '(--follow -f)'{-f,--follow}'[follow file copies and renames]' \
  '(--text -a)'{-a,--text}'[treat all files as text]' \
  '(--user -u)'{-u,--user}'[list the author]' \
  '(--date -d)'{-d,--date}'[list the date]' \
  '(--number -n)'{-n,--number}'[list the revision number (default)]' \
  '(--changeset -c)'{-c,--changeset}'[list the changeset]' \
  '*:files:_sl_files'
}

_sl_cmd_archive() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_subrepos_opts \
  '--no-decode[do not pass files through decoders]' \
  '(--prefix -p)'{-p+,--prefix=}'[directory prefix for files in archive]:' \
  '(--rev -r)'{-r+,--rev=}'[revision to distribute]:revision:_sl_labels' \
  '(--type -t)'{-t+,--type=}'[type of distribution to create]:archive type:(files tar tbz2 tgz uzip zip)' \
  '*:destination:_files'
}

_sl_cmd_backout() {
  _arguments -s -w : $_sl_global_opts $_sl_mergetool_opts $_sl_pat_opts \
    '--merge[merge with old dirstate parent after backout]' \
    '(--date -d)'{-d+,--date=}'[record datecode as commit date]:date code:' \
    '--parent[parent to choose when backing out merge]' \
    '(--user -u)'{-u+,--user=}'[record user as commiter]:user:' \
    '(--rev -r)'{-r+,--rev=}'[revision]:revision:_sl_labels' \
    '(--message -m)'{-m+,--message=}'[use <text> as commit message]:text:' \
    '(--logfile -l)'{-l+,--logfile=}'[read commit message from <file>]:log file:_files'
}

_sl_cmd_bisect() {
  _arguments -s -w : $_sl_global_opts \
  '(-)'{-r,--reset}'[reset bisect state]' \
  '(--extend -e)'{-e,--extend}'[extend the bisect range]' \
  '(--good -g --bad -b --skip -s --reset -r)'{-g,--good}'[mark changeset good]'::revision:_sl_labels \
  '(--good -g --bad -b --skip -s --reset -r)'{-b,--bad}'[mark changeset bad]'::revision:_sl_labels \
  '(--good -g --bad -b --skip -s --reset -r)'{-s,--skip}'[skip testing changeset]' \
  '(--command -c --noupdate -U)'{-c+,--command=}'[use command to check changeset state]':commands:_command_names \
  '(--command -c --noupdate -U)'{-U,--noupdate}'[do not update to target]'
}

_sl_cmd_bookmarks() {
  _arguments -s -w : $_sl_global_opts \
  '(--force -f)'{-f,--force}'[force]' \
  '(--inactive -i)'{-i,--inactive}'[mark a bookmark inactive]' \
  '(--rev -r --delete -d --rename -m)'{-r+,--rev=}'[revision]:revision:_sl_labels' \
  '(--rev -r --delete -d --rename -m)'{-d,--delete}'[delete a given bookmark]' \
  '(--rev -r --delete -d --rename -m)'{-m+,--rename=}'[rename a given bookmark]:bookmark:_sl_bookmarks' \
  ':bookmark:_sl_bookmarks'
}

_sl_cmd_branch() {
  _arguments -s -w : $_sl_global_opts \
  '(--force -f)'{-f,--force}'[set branch name even if it shadows an existing branch]' \
  '(--clean -C)'{-C,--clean}'[reset branch name to parent branch name]'
}

_sl_cmd_branches() {
  _arguments -s -w : $_sl_global_opts \
  '(--active -a)'{-a,--active}'[show only branches that have unmerge heads]' \
  '(--closed -c)'{-c,--closed}'[show normal and closed branches]'
}

_sl_cmd_bundle() {
  _arguments -s -w : $_sl_global_opts $_sl_remote_opts \
  '(--force -f)'{-f,--force}'[run even when remote repository is unrelated]' \
  '(2)*--base[a base changeset to specify instead of a destination]:revision:_sl_labels' \
  '(--branch -b)'{-b+,--branch=}'[a specific branch to bundle]:' \
  '(--rev -r)'{-r+,--rev=}'[changeset(s) to bundle]:' \
  '--all[bundle all changesets in the repository]' \
  ':output file:_files' \
  ':destination repository:_files -/'
}

_sl_cmd_cat() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts \
  '(--output -o)'{-o+,--output=}'[print output to file with formatted name]:filespec:' \
  '(--rev -r)'{-r+,--rev=}'[revision]:revision:_sl_labels' \
  '--decode[apply any matching decode filter]' \
  '*:file:_sl_files'
}

_sl_cmd_clone() {
  _arguments -s -w : $_sl_global_opts $_sl_clone_opts \
  '(--rev -r)'{-r+,--rev=}'[a changeset you would like to have after cloning]:' \
  '(--updaterev -u)'{-u+,--updaterev=}'[revision, tag or branch to check out]:' \
  '(--branch -b)'{-b+,--branch=}'[clone only the specified branch]:' \
  ':source repository:_sl_remote' \
  ':destination:_sl_clone_dest'
}

_sl_cmd_commit() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_subrepos_opts \
  '(--addremove -A)'{-A,--addremove}'[mark new/missing files as added/removed before committing]' \
  '(--message -m)'{-m+,--message=}'[use <text> as commit message]:text:' \
  '(--logfile -l)'{-l+,--logfile=}'[read commit message from <file>]:log file:_files' \
  '(--date -d)'{-d+,--date=}'[record datecode as commit date]:date code:' \
  '(--user -u)'{-u+,--user=}'[record user as commiter]:user:' \
  '--amend[amend the parent of the working dir]' \
  '--close-branch[mark a branch as closed]' \
  '*:file:_sl_files'
}

_sl_cmd_copy() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_dryrun_opts \
  '(--after -A)'{-A,--after}'[record a copy that has already occurred]' \
  '(--force -f)'{-f,--force}'[forcibly copy over an existing managed file]' \
  '*:file:_sl_files'
}

_sl_cmd_diff() {
  typeset -A opt_args
  _arguments -s -w : $_sl_global_opts $_sl_diff_opts $_sl_ignore_space_opts \
                     $_sl_pat_opts $_sl_subrepos_opts \
  '*'{-r+,--rev=}'[revision]:revision:_sl_revrange' \
  '(--show-function -p)'{-p,--show-function}'[show which function each change is in]' \
  '(--change -c)'{-c+,--change=}'[change made by revision]:' \
  '(--text -a)'{-a,--text}'[treat all files as text]' \
  '--reverse[produce a diff that undoes the changes]' \
  '(--unified -U)'{-U+,--unified=}'[number of lines of context to show]:' \
  '--stat[output diffstat-style summary of changes]' \
  '*:file:->diff_files'

  if [[ $state == 'diff_files' ]]
  then
    if [[ -n $opt_args[-r] ]]
    then
      _sl_files
    else
      _sl_modified
    fi
  fi
}

_sl_cmd_export() {
  _arguments -s -w : $_sl_global_opts $_sl_diff_opts \
  '(--outout -o)'{-o+,--output=}'[print output to file with formatted name]:filespec:' \
  '--switch-parent[diff against the second parent]' \
  '(--rev -r)'{-r+,--rev=}'[revision]:revision:_sl_labels' \
  '*:revision:_sl_labels'
}

_sl_cmd_forget() {
  _arguments -s -w : $_sl_global_opts \
  '*:file:_sl_files'
}

_sl_cmd_graft() {
  _arguments -s -w : $_sl_global_opts $_sl_dryrun_opts \
                     $_sl_date_user_opts $_sl_mergetool_opts \
  '(--continue -c)'{-c,--continue}'[resume interrupted graft]' \
  '(--edit -e)'{-e,--edit}'[invoke editor on commit messages]' \
  '--log[append graft info to log message]' \
  '*:revision:_sl_labels'
}

_sl_cmd_grep() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts \
  '(--print0 -0)'{-0,--print0}'[end filenames with NUL]' \
  '--all[print all revisions with matches]' \
  '(--follow -f)'{-f,--follow}'[follow changeset or file history]' \
  '(--ignore-case -i)'{-i,--ignore-case}'[ignore case when matching]' \
  '(--files-with-matches -l)'{-l,--files-with-matches}'[print only filenames and revs that match]' \
  '(--line-number -n)'{-n,--line-number}'[print matching line numbers]' \
  '*'{-r+,--rev=}'[search in given revision range]:revision:_sl_revrange' \
  '(--user -u)'{-u,--user}'[print user who committed change]' \
  '(--date -d)'{-d,--date}'[print date of a changeset]' \
  '1:search pattern:' \
  '*:files:_sl_files'
}

_sl_cmd_heads() {
  _arguments -s -w : $_sl_global_opts $_sl_style_opts \
  '(--topo -t)'{-t,--topo}'[show topological heads only]' \
  '(--closed -c)'{-c,--closed}'[show normal and closed branch heads]' \
  '(--rev -r)'{-r+,--rev=}'[show only heads which are descendants of rev]:revision:_sl_labels'
}

_sl_cmd_help() {
  _arguments -s -w : $_sl_global_opts \
  '(--extension -e)'{-e,--extension}'[show only help for extensions]' \
  '(--command -c)'{-c,--command}'[show only help for commands]' \
  '(--keyword -k)'{-k+,--keyword}'[show topics matching keyword]' \
  '*:mercurial help topic:_sl_help_topics'
}

_sl_cmd_identify() {
  _arguments -s -w : $_sl_global_opts $_sl_remote_opts \
  '(--rev -r)'{-r+,--rev=}'[identify the specified rev]:revision:_sl_labels' \
  '(--num -n)'{-n,--num}'[show local revision number]' \
  '(--id -i)'{-i,--id}'[show global revision id]' \
  '(--branch -b)'{-b,--branch}'[show branch]' \
  '(--bookmark -B)'{-B,--bookmark}'[show bookmarks]' \
  '(--tags -t)'{-t,--tags}'[show tags]'
}

_sl_cmd_import() {
  _arguments -s -w : $_sl_global_opts $_sl_commit_opts \
  '(--strip -p)'{-p+,--strip=}'[directory strip option for patch (default: 1)]:count:' \
  '(--force -f)'{-f,--force}'[skip check for outstanding uncommitted changes]' \
  '--bypass[apply patch without touching the working directory]' \
  '--no-commit[do not commit, just update the working directory]' \
  '--partial[commit even if some hunks fail]' \
  '--exact[apply patch to the nodes from which it was generated]' \
  '--import-branch[use any branch information in patch (implied by --exact)]' \
  '(--date -d)'{-d+,--date=}'[record datecode as commit date]:date code:' \
  '(--user -u)'{-u+,--user=}'[record user as commiter]:user:' \
  '(--similarity -s)'{-s+,--similarity=}'[guess renamed files by similarity (0<=s<=100)]:' \
  '*:patch:_files'
}

_sl_cmd_incoming() {
  _arguments -s -w : $_sl_log_opts $_sl_branch_bmark_opts $_sl_remote_opts \
                     $_sl_subrepos_opts \
  '(--force -f)'{-f,--force}'[run even when the remote repository is unrelated]' \
  '(--rev -r)'{-r+,--rev=}'[a specific revision up to which you would like to pull]:revision:_sl_labels' \
  '(--newest-first -n)'{-n,--newest-first}'[show newest record first]' \
  '--bundle[file to store the bundles into]:bundle file:_files' \
  ':source:_sl_remote'
}

_sl_cmd_init() {
  _arguments -s -w : $_sl_global_opts $_sl_remote_opts \
  ':dir:_files -/'
}

_sl_cmd_locate() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts \
  '(--rev -r)'{-r+,--rev=}'[search repository as it stood at revision]:revision:_sl_labels' \
  '(--print0 -0)'{-0,--print0}'[end filenames with NUL, for use with xargs]' \
  '(--fullpath -f)'{-f,--fullpath}'[print complete paths]' \
  '*:search pattern:_sl_files'
}

_sl_cmd_log() {
  _arguments -s -w : $_sl_log_opts $_sl_pat_opts \
  '(--follow --follow-first -f)'{-f,--follow}'[follow changeset or history]' \
  '(-f --follow)--follow-first[only follow the first parent of merge changesets]' \
  '(--copies -C)'{-C,--copies}'[show copied files]' \
  '(--keyword -k)'{-k+,--keyword}'[search for a keyword]:' \
  '*'{-r+,--rev=}'[show the specified revision or revset]:revision:_sl_revrange' \
  '(--only-merges -m)'{-m,--only-merges}'[show only merges]' \
  '(--prune -P)'{-P+,--prune=}'[do not display revision or any of its ancestors]:revision:_sl_labels' \
  '(--graph -G)'{-G,--graph}'[show the revision DAG]' \
  '(--branch -b)'{-b+,--branch=}'[show changesets within the given named branch]:branch:_sl_branches' \
  '(--user -u)'{-u+,--user=}'[revisions committed by user]:user:' \
  '(--date -d)'{-d+,--date=}'[show revisions matching date spec]:date:' \
  '*:files:_sl_files'
}

_sl_cmd_manifest() {
  _arguments -s -w : $_sl_global_opts \
  '--all[list files from all revisions]' \
  '(--rev -r)'{-r+,--rev=}'[revision to display]:revision:_sl_labels' \
  ':revision:_sl_labels'
}

_sl_cmd_merge() {
  _arguments -s -w : $_sl_global_opts $_sl_mergetool_opts \
  '(--force -f)'{-f,--force}'[force a merge with outstanding changes]' \
  '(--rev -r 1)'{-r+,--rev=}'[revision to merge]:revision:_sl_mergerevs' \
  '(--preview -P)'{-P,--preview}'[review revisions to merge (no merge is performed)]' \
  ':revision:_sl_mergerevs'
}

_sl_cmd_outgoing() {
  _arguments -s -w : $_sl_log_opts $_sl_branch_bmark_opts $_sl_remote_opts \
                     $_sl_subrepos_opts \
  '(--force -f)'{-f,--force}'[run even when the remote repository is unrelated]' \
  '*'{-r+,--rev=}'[a specific revision you would like to push]:revision:_sl_revrange' \
  '(--newest-first -n)'{-n,--newest-first}'[show newest record first]' \
  ':destination:_sl_remote'
}

_sl_cmd_parents() {
  _arguments -s -w : $_sl_global_opts $_sl_style_opts \
  '(--rev -r)'{-r+,--rev=}'[show parents of the specified rev]:revision:_sl_labels' \
  ':last modified file:_sl_files'
}

_sl_cmd_paths() {
  _arguments -s -w : $_sl_global_opts \
  ':path:_sl_paths'
}

_sl_cmd_phase() {
  _arguments -s -w : $_sl_global_opts \
  '(--public -p)'{-p,--public}'[set changeset phase to public]' \
  '(--draft -d)'{-d,--draft}'[set changeset phase to draft]' \
  '(--secret -s)'{-s,--secret}'[set changeset phase to secret]' \
  '(--force -f)'{-f,--force}'[allow to move boundary backward]' \
  '(--rev -r)'{-r+,--rev=}'[target revision]:revision:_sl_labels' \
  ':revision:_sl_labels'
}

_sl_cmd_pull() {
  _arguments -s -w : $_sl_global_opts $_sl_branch_bmark_opts $_sl_remote_opts \
  '(--force -f)'{-f,--force}'[run even when the remote repository is unrelated]' \
  '(--update -u)'{-u,--update}'[update to new tip if changesets were pulled]' \
  '(--rev -r)'{-r+,--rev}'[a specific revision up to which you would like to pull]:revision:' \
  ':source:_sl_remote'
}

_sl_cmd_push() {
  _arguments -s -w : $_sl_global_opts $_sl_branch_bmark_opts $_sl_remote_opts \
  '(--force -f)'{-f,--force}'[force push]' \
  '(--rev -r)'{-r+,--rev=}'[a specific revision you would like to push]:revision:_sl_labels' \
  '--new-branch[allow pushing a new branch]' \
  ':destination:_sl_remote'
}

_sl_cmd_remove() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts \
  '(--after -A)'{-A,--after}'[record remove that has already occurred]' \
  '(--force -f)'{-f,--force}'[remove file even if modified]' \
  '*:file:_sl_files'
}

_sl_cmd_rename() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_dryrun_opts \
  '(--after -A)'{-A,--after}'[record a rename that has already occurred]' \
  '(--force -f)'{-f,--force}'[forcibly copy over an existing managed file]' \
  '*:file:_sl_files'
}

_sl_cmd_resolve() {
  local context state line
  typeset -A opt_args

  _arguments -s -w : $_sl_global_opts $_sl_mergetool_opts $_sl_pat_opts \
  '(--all -a)'{-a,--all}'[select all unresolved files]' \
  '(--no-status -n)'{-n,--no-status}'[hide status prefix]' \
  '(--list -l --mark -m --unmark -u)'{-l,--list}'[list state of files needing merge]:*:merged files:->resolve_files' \
  '(--mark -m --list -l --unmark -u)'{-m,--mark}'[mark files as resolved]:*:unresolved files:_sl_unresolved' \
  '(--unmark -u --list -l --mark -m)'{-u,--unmark}'[unmark files as resolved]:*:resolved files:_sl_resolved' \
  '*:file:_sl_unresolved'

  if [[ $state == 'resolve_files' ]]
  then
    _alternative 'files:resolved files:_sl_resolved' \
      'files:unresolved files:_sl_unresolved'
  fi
}

_sl_cmd_revert() {
  local context state line
  typeset -A opt_args

  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_dryrun_opts \
  '(--all -a :)'{-a,--all}'[revert all changes when no arguments given]' \
  '(--rev -r)'{-r+,--rev=}'[revision to revert to]:revision:_sl_labels' \
  '(--no-backup -C)'{-C,--no-backup}'[do not save backup copies of files]' \
  '(--date -d)'{-d+,--date=}'[tipmost revision matching date]:date code:' \
  '*:file:->diff_files'

  if [[ $state == 'diff_files' ]]
  then
    if [[ -n $opt_args[-r] ]]
    then
      _sl_files
    else
      typeset -a status_files
      _sl_status mard
      _wanted files expl 'modified, added, removed or deleted file' _multi_parts / status_files
    fi
  fi
}

_sl_cmd_rollback() {
  _arguments -s -w : $_sl_global_opts $_sl_dryrun_opts \
  '(--force -f)'{-f,--force}'[ignore safety measures]' \
}

_sl_cmd_serve() {
  _arguments -s -w : $_sl_global_opts \
  '(--accesslog -A)'{-A+,--accesslog=}'[name of access log file]:log file:_files' \
  '(--errorlog -E)'{-E+,--errorlog=}'[name of error log file]:log file:_files' \
  '(--daemon -d)'{-d,--daemon}'[run server in background]' \
  '(--port -p)'{-p+,--port=}'[listen port]:listen port:' \
  '(--address -a)'{-a+,--address=}'[interface address]:interface address:' \
  '--prefix[prefix path to serve from]:directory:_files' \
  '(--name -n)'{-n+,--name=}'[name to show in web pages]:repository name:' \
  '--pid-file[name of file to write process ID to]:pid_file:_files' \
  '--cmdserver[cmdserver mode]:mode:' \
  '(--templates -t)'{-t,--templates}'[web template directory]:template dir:_files -/' \
  '--style[web template style]:style' \
  '--stdio[for remote clients]' \
  '--certificate[certificate file]:cert_file:_files' \
  '(--ipv6 -6)'{-6,--ipv6}'[use IPv6 in addition to IPv4]'
}

_sl_cmd_showconfig() {
  _arguments -s -w : $_sl_global_opts \
  '(--untrusted -u)'{-u,--untrusted}'[show untrusted configuration options]' \
  ':config item:_sl_config'
}

_sl_cmd_status() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_subrepos_opts \
  '(--all -A)'{-A,--all}'[show status of all files]' \
  '(--modified -m)'{-m,--modified}'[show only modified files]' \
  '(--added -a)'{-a,--added}'[show only added files]' \
  '(--removed -r)'{-r,--removed}'[show only removed files]' \
  '(--deleted -d)'{-d,--deleted}'[show only deleted (but tracked) files]' \
  '(--clean -c)'{-c,--clean}'[show only files without changes]' \
  '(--unknown -u)'{-u,--unknown}'[show only unknown files]' \
  '(--ignored -i)'{-i,--ignored}'[show ignored files]' \
  '(--no-status -n)'{-n,--no-status}'[hide status prefix]' \
  '(--copies -C)'{-C,--copies}'[show source of copied files]' \
  '(--print0 -0)'{-0,--print0}'[end filenames with NUL, for use with xargs]' \
  '--rev[show difference from revision]:revision:_sl_labels' \
  '--change[list the changed files of a revision]:revision:_sl_labels' \
  '*:files:_files'
}

_sl_cmd_summary() {
  _arguments -s -w : $_sl_global_opts \
  '--remote[check for push and pull]'
}

_sl_cmd_tag() {
  _arguments -s -w : $_sl_global_opts \
  '(--local -l)'{-l,--local}'[make the tag local]' \
  '(--message -m)'{-m+,--message=}'[message for tag commit log entry]:message:' \
  '(--date -d)'{-d+,--date=}'[record datecode as commit date]:date code:' \
  '(--user -u)'{-u+,--user=}'[record user as commiter]:user:' \
  '(--rev -r)'{-r+,--rev=}'[revision to tag]:revision:_sl_labels' \
  '(--force -f)'{-f,--force}'[force tag]' \
  '--remove[remove a tag]' \
  '(--edit -e)'{-e,--edit}'[edit commit message]' \
  ':tag name:'
}

_sl_cmd_tip() {
  _arguments -s -w : $_sl_global_opts $_sl_gitlike_opts $_sl_style_opts \
  '(--patch -p)'{-p,--patch}'[show patch]'
}

_sl_cmd_unbundle() {
  _arguments -s -w : $_sl_global_opts \
  '(--update -u)'{-u,--update}'[update to new tip if changesets were unbundled]' \
  ':files:_files'
}

_sl_cmd_update() {
  _arguments -s -w : $_sl_global_opts \
  '(--clean -C)'{-C,--clean}'[overwrite locally modified files]' \
  '(--rev -r)'{-r+,--rev=}'[revision]:revision:_sl_labels' \
  '(--check -c)'{-c,--check}'[update across branches if no uncommitted changes]' \
  '(--date -d)'{-d+,--date=}'[tipmost revision matching date]:' \
  ':revision:_sl_labels'
}

## extensions ##

# slK
_sl_cmd_view() {
  _arguments -s -w : $_sl_global_opts \
  '(--limit -l)'{-l+,--limit=}'[limit number of changes displayed]:' \
  ':revision range:_sl_labels'
}

# MQ
_sl_qseries() {
  typeset -a patches
  patches=(${(f)"$(_sl_cmd qseries)"})
  (( $#patches )) && _describe -t sl-patches 'patches' patches
}

_sl_qapplied() {
  typeset -a patches
  patches=(${(f)"$(_sl_cmd qapplied)"})
  if (( $#patches ))
  then
    patches+=(qbase qtip)
    _describe -t sl-applied-patches 'applied patches' patches
  fi
}

_sl_qunapplied() {
  typeset -a patches
  patches=(${(f)"$(_sl_cmd qunapplied)"})
  (( $#patches )) && _describe -t sl-unapplied-patches 'unapplied patches' patches
}

# unapplied, including guarded patches
_sl_qdeletable() {
  typeset -a unapplied
  unapplied=(${(f)"$(_sl_cmd qseries)"})
  for p in $(_sl_cmd qapplied)
  do
    unapplied=(${unapplied:#$p})
  done

  (( $#unapplied )) && _describe -t sl-allunapplied-patches 'all unapplied patches' unapplied
}

_sl_qguards() {
  typeset -a guards
  local guard
  compset -P "+|-"
  _sl_cmd qselect -s | while read guard
  do
    guards+=(${guard#(+|-)})
  done
  (( $#guards )) && _describe -t sl-guards 'guards' guards
}

_sl_qseries_opts=(
  '(--summary -s)'{-s,--summary}'[print first line of patch header]')

_sl_cmd_qapplied() {
  _arguments -s -w : $_sl_global_opts $_sl_qseries_opts \
  '(--last -1)'{-1,--last}'[show only the preceding applied patch]' \
  '*:patch:_sl_qapplied'
}

_sl_cmd_qclone() {
  _arguments -s -w : $_sl_global_opts $_sl_remote_opts $_sl_clone_opts \
  '(--patches -p)'{-p+,--patches=}'[location of source patch repository]:' \
  ':source repository:_sl_remote' \
  ':destination:_sl_clone_dest'
}

_sl_cmd_qdelete() {
  _arguments -s -w : $_sl_global_opts \
  '(--keep -k)'{-k,--keep}'[keep patch file]' \
  '*'{-r+,--rev=}'[stop managing a revision]:applied patch:_sl_revrange' \
  '*:unapplied patch:_sl_qdeletable'
}

_sl_cmd_qdiff() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_diff_opts \
                     $_sl_ignore_space_opts \
  '*:pattern:_sl_files'
}

_sl_cmd_qfinish() {
  _arguments -s -w : $_sl_global_opts \
  '(--applied -a)'{-a,--applied}'[finish all applied patches]' \
  '*:patch:_sl_qapplied'
}

_sl_cmd_qfold() {
  _arguments -s -w : $_sl_global_opts $_h_commit_opts \
  '(--keep,-k)'{-k,--keep}'[keep folded patch files]' \
  '(--force -f)'{-f,--force}'[overwrite any local changes]' \
  '--no-backup[do not save backup copies of files]' \
  '*:unapplied patch:_sl_qunapplied'
}

_sl_cmd_qgoto() {
  _arguments -s -w : $_sl_global_opts \
  '(--force -f)'{-f,--force}'[overwrite any local changes]' \
  '--keep-changes[tolerate non-conflicting local changes]' \
  ':patch:_sl_qseries'
}

_sl_cmd_qguard() {
  _arguments -s -w : $_sl_global_opts \
  '(--list -l)'{-l,--list}'[list all patches and guards]' \
  '(--none -n)'{-n,--none}'[drop all guards]' \
  ':patch:_sl_qseries' \
  '*:guards:_sl_qguards'
}

_sl_cmd_qheader() {
  _arguments -s -w : $_sl_global_opts \
  ':patch:_sl_qseries'
}

_sl_cmd_qimport() {
  _arguments -s -w : $_sl_global_opts $_sl_gitlike_opts \
  '(--existing -e)'{-e,--existing}'[import file in patch dir]' \
  '(--name -n 2)'{-n+,--name}'[patch file name]:name:' \
  '(--force -f)'{-f,--force}'[overwrite existing files]' \
  '*'{-r+,--rev=}'[place existing revisions under mq control]:revision:_sl_revrange' \
  '(--push -P)'{-P,--push}'[qpush after importing]' \
  '*:patch:_files'
}

_sl_cmd_qnew() {
  _arguments -s -w : $_sl_global_opts $_sl_commit_opts $_sl_date_user_opts $_sl_gitlike_opts \
  ':patch:'
}

_sl_cmd_qnext() {
  _arguments -s -w : $_sl_global_opts $_sl_qseries_opts
}

_sl_cmd_qpop() {
  _arguments -s -w : $_sl_global_opts \
  '(--all -a :)'{-a,--all}'[pop all patches]' \
  '(--force -f)'{-f,--force}'[forget any local changes]' \
  '--keep-changes[tolerate non-conflicting local changes]' \
  '--no-backup[do not save backup copies of files]' \
  ':patch:_sl_qapplied'
}

_sl_cmd_qprev() {
  _arguments -s -w : $_sl_global_opts $_sl_qseries_opts
}

_sl_cmd_qpush() {
  _arguments -s -w : $_sl_global_opts \
  '(--all -a :)'{-a,--all}'[apply all patches]' \
  '(--list -l)'{-l,--list}'[list patch name in commit text]' \
  '(--force -f)'{-f,--force}'[apply if the patch has rejects]' \
  '(--exact -e)'{-e,--exact}'[apply the target patch to its recorded parent]' \
  '--move[reorder patch series and apply only the patch]' \
  '--keep-changes[tolerate non-conflicting local changes]' \
  '--no-backup[do not save backup copies of files]' \
  ':patch:_sl_qunapplied'
}

_sl_cmd_qrefresh() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_commit_opts $_sl_gitlike_opts \
  '(--short -s)'{-s,--short}'[short refresh]' \
  '*:files:_sl_files'
}

_sl_cmd_qrename() {
  _arguments -s -w : $_sl_global_opts \
  ':patch:_sl_qunapplied' \
  ':destination:'
}

_sl_cmd_qselect() {
  _arguments -s -w : $_sl_global_opts \
  '(--none -n :)'{-n,--none}'[disable all guards]' \
  '(--series -s :)'{-s,--series}'[list all guards in series file]' \
  '--pop[pop to before first guarded applied patch]' \
  '--reapply[pop and reapply patches]' \
  '*:guards:_sl_qguards'
}

_sl_cmd_qseries() {
  _arguments -s -w : $_sl_global_opts $_sl_qseries_opts \
  '(--missing -m)'{-m,--missing}'[print patches not in series]'
}

_sl_cmd_qunapplied() {
  _arguments -s -w : $_sl_global_opts $_sl_qseries_opts \
  '(--first -1)'{-1,--first}'[show only the first patch]'
}

_sl_cmd_qtop() {
  _arguments -s -w : $_sl_global_opts $_sl_qseries_opts
}

_sl_cmd_strip() {
  _arguments -s -w : $_sl_global_opts \
  '(--force -f)'{-f,--force}'[force removal, discard uncommitted changes, no backup]' \
  '(--no-backup -n)'{-n,--no-backup}'[no backups]' \
  '(--keep -k)'{-k,--keep}'[do not modify working copy during strip]' \
  '(--bookmark -B)'{-B+,--bookmark=}'[remove revs only reachable from given bookmark]:bookmark:_sl_bookmarks' \
  '(--rev -r)'{-r+,--rev=}'[revision]:revision:_sl_labels' \
  ':revision:_sl_labels'
}

# Patchbomb
_sl_cmd_email() {
  _arguments -s -w : $_sl_global_opts $_sl_remote_opts $_sl_gitlike_opts \
  '--plain[omit sl patch header]' \
  '--body[send patches as inline message text (default)]' \
  '(--outgoing -o)'{-o,--outgoing}'[send changes not found in the target repository]' \
  '(--bundle -b)'{-b,--bundle}'[send changes not in target as a binary bundle]' \
  '--bundlename[name of the bundle attachment file (default: bundle)]:' \
  '*'{-r+,--rev=}'[search in given revision range]:revision:_sl_revrange' \
  '--force[run even when remote repository is unrelated (with -b/--bundle)]' \
  '*--base[a base changeset to specify instead of a destination (with -b/--bundle)]:revision:_sl_labels' \
  '--intro[send an introduction email for a single patch]' \
  '(--inline -i --attach -a)'{-a,--attach}'[send patches as attachments]' \
  '(--attach -a --inline -i)'{-i,--inline}'[send patches as inline attachments]' \
  '*--bcc[email addresses of blind carbon copy recipients]:email:' \
  '*'{-c+,--cc}'[email addresses of copy recipients]:email:' \
  '(--diffstat -d)'{-d,--diffstat}'[add diffstat output to messages]' \
  '--date[use the given date as the sending date]:date:' \
  '--desc[use the given file as the series description]:files:_files' \
  '(--from -f)'{-f,--from}'[email address of sender]:email:' \
  '(--test -n)'{-n,--test}'[print messages that would be sent]' \
  '(--mbox -m)'{-m,--mbox}'[write messages to mbox file instead of sending them]:file:' \
  '*--reply-to[email addresses replies should be sent to]:email:' \
  '(--subject -s)'{-s,--subject}'[subject of first message (intro or single patch)]:subject:' \
  '--in-reply-to[message identifier to reply to]:msgid:' \
  '*--flag[flags to add in subject prefixes]:flag:' \
  '*'{-t,--to}'[email addresses of recipients]:email:' \
  ':revision:_sl_revrange'
}

# Rebase
_sl_cmd_rebase() {
  _arguments -s -w : $_sl_global_opts $_sl_commit_opts $_sl_mergetool_opts \
  '*'{-r+,--rev=}'[rebase these revisions]:revision:_sl_revrange' \
  '(--source -s)'{-s+,--source=}'[rebase from the specified changeset]:revision:_sl_labels' \
  '(--base -b)'{-b+,--base=}'[rebase from the base of the specified changeset]:revision:_sl_labels' \
  '(--dest -d)'{-d+,--dest=}'[rebase onto the specified changeset]:revision:_sl_labels' \
  '--collapse[collapse the rebased changeset]' \
  '--keep[keep original changeset]' \
  '--keepbranches[keep original branch name]' \
  '(--continue -c)'{-c,--continue}'[continue an interrupted rebase]' \
  '(--abort -a)'{-a,--abort}'[abort an interrupted rebase]' \
}

# Record
_sl_cmd_record() {
  _arguments -s -w : $_sl_global_opts $_sl_commit_opts $_sl_pat_opts \
                     $_sl_ignore_space_opts $_sl_subrepos_opts \
  '(--addremove -A)'{-A,--addremove}'[mark new/missing files as added/removed before committing]' \
  '--close-branch[mark a branch as closed, hiding it from the branch list]' \
  '--amend[amend the parent of the working dir]' \
  '(--date -d)'{-d+,--date=}'[record the specified date as commit date]:date:' \
  '(--user -u)'{-u+,--user=}'[record the specified user as committer]:user:'
}

_sl_cmd_qrecord() {
  _arguments -s -w : $_sl_global_opts $_sl_commit_opts $_sl_date_user_opts $_sl_gitlike_opts \
                     $_sl_pat_opts $_sl_ignore_space_opts $_sl_subrepos_opts
}

# Convert
_sl_cmd_convert() {
_arguments -s -w : $_sl_global_opts \
  '(--source-type -s)'{-s,--source-type}'[source repository type]' \
  '(--dest-type -d)'{-d,--dest-type}'[destination repository type]' \
  '(--rev -r)'{-r+,--rev=}'[import up to target revision]:revision:' \
  '(--authormap -A)'{-A+,--authormap=}'[remap usernames using this file]:file:_files' \
  '--filemap[remap file names using contents of file]:file:_files' \
  '--splicemap[splice synthesized history into place]:file:_files' \
  '--branchmap[change branch names while converting]:file:_files' \
  '--branchsort[try to sort changesets by branches]' \
  '--datesort[try to sort changesets by date]' \
  '--sourcesort[preserve source changesets order]'
}

# Graphlog
_sl_cmd_glog() {
  _sl_cmd_log $@
}

# Purge
_sl_cmd_purge() {
  _arguments -s -w : $_sl_global_opts $_sl_pat_opts $_sl_subrepos_opts \
  '(--abort-on-err -a)'{-a,--abort-on-err}'[abort if an error occurs]' \
  '--all[purge ignored files too]' \
  '(--print -p)'{-p,--print}'[print filenames instead of deleting them]' \
  '(--print0 -0)'{-0,--print0}'[end filenames with NUL, for use with xargs (implies -p/--print)]'
}

# Histedit
_sl_cmd_histedit() {
  _arguments -s -w : $_sl_global_opts \
  '(--continue -c)'{-c,--continue}'[continue an interrupted histedit]' \
  '(--abort -a)'{-a,--abort}'[abort an interrupted histedit]' \
}

_sl "$@"
